
model {
  for (j in 1:J){                ## loop over diputados
    for (i in 1:I){              ## loop over items
      #y.hat[j,i] ~ dbern(p[j,i]);                                  ## voting rule
      #p[j,i] <- phi(v.star[j,i]);                                  ## sets 0<p<1
      v.star[j,i] ~ dnorm(mu[j,i],1)I(lo.v[j,i],hi.v[j,i]);   ## truncated normal sampling
      mu[j,i] <- delta[i]*a[i]*x[j] + delta[i]*b[i] - delta[i]*y[j] ## utility differential
                  }
                }
  for (i in 1:I){
  a[i] <- sin(angle[i]) / sqrt(1-sin(angle[i])*sin(angle[i]))
  }
  ## priors ################
for (j in 1:(N-1)){
    x[j] ~  dnorm(0, 1)
    y[j] ~  dnorm(0, 1)
    }
    x[N] ~  dnorm(0, 4)    # Mrs. NORTH Piña Olmedo Laura (PRD)
    y[N] ~  dnorm(2, 4)
#    x[N] <- 0
#    y[N] <- 2
for (j in (N+1):(W-1)){
    x[j] ~  dnorm(0, 1)
    y[j] ~  dnorm(0, 1)
    }
    x[W] ~  dnorm(-2, 4)   # Mr. WEST Méndez Rangel Avelino (PRD)
    y[W] ~  dnorm(0, 4)
#    x[W] <- -2
#    y[W] <- 0
for (j in (W+1):(E-1)){
    x[j] ~  dnorm(0, 1)
    y[j] ~  dnorm(0, 1)
    }
    x[E] ~  dnorm(2, 4)    # Mrs. EAST Paula Adriana Soto Maldonado (PAN)
    y[E] ~  dnorm(0, 4)
#    x[E] <- 2
#    y[E] <- 0
for (j in (E+1):(S-1)){
    x[j] ~  dnorm(0, 1)
    y[j] ~  dnorm(0, 1)
    }
    x[S] ~  dnorm(0, 4)    # Mr. SOUTH Tenorio Antiga Xiuh (PANAL)
    y[S] ~  dnorm(-2, 4)
#    x[S] <- 0
#    y[S] <- -2
for (j in (S+1):J){
    x[j] ~  dnorm(0, 1)
    y[j] ~  dnorm(0, 1)
    }
    for(i in 1:I){
        delta[i] ~ dnorm( 0, 0.1)
        angle[i] ~ dunif(-1.57,1.57) # (-pi/2,pi/2)
        b[i] ~ dnorm( 0, .1)
                 }
}
